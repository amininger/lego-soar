###################################################
# This file contains the following operators:
#   record-red
#   turn-180
#   record-turn-complete
#   swap-colors
#   move-forward
#   clear-flags
###################################################

source Stage2-completed.soar

###################################################
#
# elaborations
#
##################################################

# Copy the left and right motor sensor values from the input-link
sp {Stage3*elaborate*motor-sensor-values
	(state <s> ^name line-follower
						 ^right-motor-port <right-port>
						 ^left-motor-port <left-port>
						 ^io.input-link <il>)
	(<il> ^motor <left>
				^motor <right>)
	(<left> ^port <left-port>
					^amount-rotated <left-val>)
	(<right> ^port <right-port>
					 ^amount-rotated <right-val>)
-->
	(<s> ^left-motor-sensor-value <left-val>
			 ^right-motor-sensor-value <right-val>)
}

###################################################
#
# preferences
#
##################################################

# Prefer all the operators related to turning 180
#   over the follow-line operator
sp {Stage3*prefer*180*operators*over*follow-line
	(state <s> ^name line-follower
						 ^operator <o1> +
						 ^operator <o2> +)
	(<o1> ^name << record-red turn-180 record-turn-complete swap-colors move-forward clear-flags >>)
	(<o2> ^name follow-line)
-->
	(<s> ^operator <o1> > <o2>)
}

##################################################
#
# operator record-red
#   when we see red, add a ^detected-red flag to topstate
#
#################################################

sp {Stage3*propose*record-red
	(state <s> ^name line-follower
						 ^color-sensor-value red
						 -^detected-red)
-->
	(<s> ^operator <o> +)
	(<o> ^name record-red
			 ^action stop)
}

sp {Stage3*apply*record-red
	(state <s> ^name line-follower
						 ^operator.name record-red)
-->
	(<s> ^detected-red true)
}

###################################################
#
# operator turn-180
#  When the robot has detected red, it must first rotate 180
#
###################################################

sp {Stage3*propose*turn-180
	(state <s> ^name line-follower
						 ^detected-red true
						 -^turn-complete)
-->
	(<s> ^operator <o> +)
	(<o> ^name turn-180)
}

# Start the rotation.  Arbitrarily chose to start by
# rotating left.  However, because we're rotating 180
# one could choose to rotate right.
sp {Stage3*apply*turn-180*motor-commands
	(state <s>	^name line-follower
						 ^operator.name turn-180
						 ^io.output-link <out>
						 ^left-motor-port <left-port>
						 ^right-motor-port <right-port>)
-->
	(<out>	^motor <left>
			^motor <right>)
	(<left>	^port <left-port>
					^set-direction forward
					^set-power 40)
	(<right> ^port <right-port>
					 ^set-direction backward
					 ^set-power 40)
}

# Record the desired sensor value for having turned 180 degrees
sp {Stage3*apply*turn-180*record*180-right-motor-sensor-value
	(state <s> ^name line-follower
						 ^operator.name turn-180
						 -^180-right-motor-sensor-value
						 ^right-motor-sensor-value <right-val>)
-->
	(<s> ^180-right-motor-sensor-value (- <right-val> 1000))
}

##################################################
#
# operator record-turn-complete
#
##################################################

# If the right motor sensor value is greater than the 180 value
#   Then propose record-turn-complete
sp {Stage3*propose*record-turn-complete
	(state <s> ^name line-follower
						 ^180-right-motor-sensor-value <desired-val>
						 ^right-motor-sensor-value <= <desired-val>
						 -^turn-complete)
-->
	(<s> ^operator <o> + >)
	(<o> ^name record-turn-complete)
}

# If the record-turn-complete operator is selected
#   Then add the turn-complete true flag to the topstate
sp {Stage3*apply*record-turn-complete
	(state <s> ^name line-follower
						 ^operator.name record-turn-complete)
-->
	(<s> ^turn-complete true)
}

##################################################
#
# operator swap-colors
#
##################################################

# If we have finished turning and have not yet swapped colors,
#   Then propose swap colors
sp {Stage3*propose*swap-colors
	(state <s> ^name line-follower
						 ^turn-complete true
						 -^swapped-colors true)
-->
	(<s> ^operator <o> +)
	(<o> ^name swap-colors
			 ^action stop)
}

# If the swap-colors operator is selected
#   Then exchange color-left and color-right
#     and add swapped-colors true to the topstate
sp {Stage3*apply*swap-colors
	(state <s> ^name line-follower
						 ^operator.name swap-colors
						 ^color-left <left>
						 ^color-right <right>)
-->
	(<s> ^color-left <left> -
			 ^color-left <right>
			 ^color-right <right> -
			 ^color-right <left>
			 ^swapped-colors true)
}

##################################################
#
# operator move-forward
#
##################################################

# If we have swapped the colors and are still on red,
#   propose move-forward
sp {Stage3*propose*move-forward
	(state <s> ^name line-follower
						 ^swapped-colors true
						 ^color-sensor-value red)
-->
	(<s> ^operator <o> +)
	(<o> ^name move-forward)
}

# Application rule already coded in Stage2-complete.soar


##################################################
#
# operator clear-flags 
#
##################################################

# If we have swapped colors and are no longer on red,
#   Then propose clear-flags
sp {Stage3*propose*clear-flags
	(state <s> ^name line-follower
						 ^color-sensor-value <> red
						 ^swapped-colors true)
-->
	(<s> ^operator <o> + >)
	(<o> ^name clear-flags)
}

# If the clear-flags operator is selected
#   Then remove the following flags from the topstate
#    detected-red, 180-right-motor-sensor-value, 
#    turn-complete, swapped-colors
sp {Stage3*apply*clear-flags
	(state <s> ^name line-follower
						 ^operator.name clear-flags
						 ^detected-red <dr>
						 ^180-right-motor-sensor-value <rmsv>
						 ^turn-complete <tc>
						 ^swapped-colors <sc>)
-->
	(<s> ^detected-red <dr> -
			 ^180-right-motor-sensor-value <rmsv> -
			 ^turn-complete <tc> -
			 ^swapped-colors <sc> -)
}

